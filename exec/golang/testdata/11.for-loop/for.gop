// for with <-
sum := 0
for x <- [1, 3, 5, 7, 11, 13, 17], x > 3 {
	sum += x
}
println("sum(5,7,11,13,17):", sum)

fns := make([]func() int, 3)
for i, x <- [3, 15, 777] {
	v := x
	fns[i] = func() int {
		return v
	}
}
println("values:", fns[0](), fns[1](), fns[2]())

// for with range
sum = 0
arr := [1, 3, 5, 7]
m := {1: 1, 2: 4, 3: 8}
for range arr {
	sum++
}
println("sum==4", sum == 4)

sum = 0
for x := range arr {
	sum += x
}
println("sum==6", sum == 6)

sum = 0
for k, x := range m {
	sum += x + k
}
println("sum==19", sum == 19)

sum = 0
rangeX := 0
for _, rangeX = range [1, 3, 5, 7, 11] {
	if rangeX > 3 {
		sum += rangeX
	}
}
println("rangeX:", rangeX == 11, rangeX)
println("sum(1, 3, 5, 7, 11):", sum == 23, sum)

sum = 0
rangeX = 0
for _, rangeX = range [1, 3, 5] {
	sum += rangeX
	for rangeX, _ = range [0, 2] {
		sum += rangeX
	}
}
println("rangeX:", rangeX == 1, rangeX)
println("sum(1, 3, 5)(0,2):", sum == 12, sum)

for _, _ = range [1, 3, 5, 7] {
}

// normal for
i := 10
sum = 0
for i = 0; i < len(arr); i++ {
	sum += arr[i]
}
println("sum==16", sum == 16, sum)

sum = 0
for i := 1; i < len(arr); i++ {
	sum += arr[i]
}
println("sum==15", sum == 15, sum)

i = 0
sum = 0
for ; i < len(arr); i += 2 {
	sum += arr[i]
}
println("sum==6", sum == 6, sum)

sum = 0
for i = 0; i < len(arr); {
	sum += arr[i]
	i++
}
println("sum==16", sum == 16, sum)

// normal for with break and continue
sum = 0
for i := 0; i < len(arr); i++ {
	if arr[i] > 5 {
		break
	}
	for j := 0; j < len(arr); j++ {
		if arr[j] < 5 {
			continue
		}
		sum += arr[j]
	}
}
println("sum==36", sum == 36, sum)

for1()
for2()
for3()
for4()

// issues #545
for k <- [1, 2] {
	println(k, (k+1)%2)
}
